#DEFINITIONS:  -*-sh-*-
#
#   Performas an all-sky calculation
#
# 2020-12-18 Patrick Eriksson
Arts2{

  INCLUDE "general/general.arts"
  INCLUDE "general/agendas.arts"
  INCLUDE "general/planet_earth.arts"

  # Read files derived from ERA5
  ReadXML( p_grid, "p_grid.xml" )
  ReadXML( lat_grid, "lat_grid.xml" )
  ReadXML( lat_true, "lat_true.xml" )
  ReadXML( lon_true, "lon_true.xml" )
  ReadXML( z_field, "z_field.xml" )
  ReadXML( t_field, "t_field.xml" )
  ReadXML( vmr_field, "vmr_field.xml" )
  ReadXML( z_surface, "z_surface.xml" )

  # Some basic settings  
  AtmosphereSet2D
  ReadXML( f_grid, "f_grid.xml" )
  ReadXML( stokes_dim, "stokes_dim.xml" )

  # Species and absorption
  INCLUDE "absorption.arts"
    
  # Check atmospheric fields
  atmfields_checkedCalc( bad_partition_functions_ok = 1 )
  atmgeom_checkedCalc

  # Import single scattering data
  ReadXML( scat_data_raw, "scat_data_raw.xml" )
  ReadXML( scat_meta, "scat_meta.xml" )
  scat_dataCalc
  scat_data_checkedCalc

  # PSDs
  NumericCreate( phase_tlim )
  ReadXML( phase_tlim, "phase_tlim.xml" )
  INCLUDE "psd_rwc.arts"
  INCLUDE "psd_iwc.arts"

  # Create inversion table
  ArrayOfGriddedField3Create( invtable )
  VectorCreate( dbze_grid )
  VectorCreate( t_grid )
  VectorLinSpace( dbze_grid, -30, 22, 1 )
  # Rain
  VectorLinSpace( t_grid, 274, 330, 1 )
  RadarOnionPeelingTableCalc( invtable = invtable,
                              i_species = 0,
                              dbze_grid = dbze_grid,
                              t_grid = t_grid)
  # Ice
  VectorLinSpace( t_grid, 210, 273, 1 )
  RadarOnionPeelingTableCalc( invtable = invtable,
                              i_species = 1,
                              dbze_grid = dbze_grid,
                              t_grid = t_grid)

}
